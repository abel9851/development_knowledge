```hdl
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/b/RAM512.hdl
/**
 * Memory of 512 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    DMux8Way(
        in=load,
        sel=address[6..8],
        a=ram64ALoadIn,
        b=ram64BLoadIn,
        c=ram64CLoadIn,
        d=ram64DLoadIn,
        e=ram64ELoadIn,
        f=ram64FLoadIn,
        g=ram64GLoadIn,
        h=ram64HLoadIn
    );

    RAM64(in=in, load=ram64ALoadIn, address=address[0..5], out=ram64AOut);
    RAM64(in=in, load=ram64BLoadIn, address=address[0..5], out=ram64BOut);
    RAM64(in=in, load=ram64CLoadIn, address=address[0..5], out=ram64COut);
    RAM64(in=in, load=ram64DLoadIn, address=address[0..5], out=ram64DOut);
    RAM64(in=in, load=ram64ELoadIn, address=address[0..5], out=ram64EOut);
    RAM64(in=in, load=ram64FLoadIn, address=address[0..5], out=ram64FOut);
    RAM64(in=in, load=ram64GLoadIn, address=address[0..5], out=ram64GOut);
    RAM64(in=in, load=ram64HLoadIn, address=address[0..5], out=ram64HOut);

    Mux8Way16(
        a=ram64AOut,
        b=ram64BOut,
        c=ram64COut,
        d=ram64DOut,
        e=ram64EOut,
        f=ram64FOut,
        g=ram64GOut,
        h=ram64HOut,
        sel=address[6..8],
        out=out
    );
}
```
- 8(3) -> 64(6) -> 512(9) -> 4096(12)(4k) -> 16384(14)(16k)

#Nand2Tetris #기초