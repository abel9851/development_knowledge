```hdl


// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/b/RAM64.hdl
/**
 * Memory of 64 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    DMux8Way(
        in=load,
        sel=address[3..5],
        a=ramALoadIn,
        b=ramBLoadIn,
        c=ramCLoadIn,
        d=ramDLoadIn,
        e=ramELoadIn,
        f=ramFLoadIn,
        g=ramGLoadIn,
        h=ramHLoadIn
    );

    RAM8(in=in,load=ramALoadIn, address=address[0..2], out=ramAOut);
    RAM8(in=in,load=ramBLoadIn, address=address[0..2], out=ramBOut);
    RAM8(in=in,load=ramCLoadIn, address=address[0..2], out=ramCOut);
    RAM8(in=in,load=ramDLoadIn, address=address[0..2], out=ramDOut);
    RAM8(in=in,load=ramELoadIn, address=address[0..2], out=ramEOut);
    RAM8(in=in,load=ramFLoadIn, address=address[0..2], out=ramFOut);
    RAM8(in=in,load=ramGLoadIn, address=address[0..2], out=ramGOut);
    RAM8(in=in,load=ramHLoadIn, address=address[0..2], out=ramHOut);
    
    Mux8Way16(
        a=ramAOut,
        b=ramBOut,
        c=ramCOut,
        d=ramDOut,
        e=ramEOut,
        f=ramFOut,
        g=ramGOut,
        h=ramHOut,
        sel=address[3..5],
        out=out
    ); 
}

```